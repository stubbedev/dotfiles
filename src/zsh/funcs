#!/usr/bin/env zsh

function src-stb-installers {
  source "$(readlink -f "$HOME/.stubbe/bin/install-funcs")"
}

function is_binary {
  command -v "$1" &>/dev/null
}

function is_directory {
  [[ -d "$1" ]]
}

function is_file {
  [[ -f "$1" ]]
}

function has_string {
  grep -q "$2" "$1"
}

function is_elevated_shell {
  [[ "$EUID" -eq 0 ]]
}

function src_zsh {
  source "$HOME/.zshrc"
}

if is_binary php; then
  function art {
    if is_file "$PWD/artisan"; then
      php artisan "${@:1}"
    elif is_binary artisan; then
      artisan "${@:1}"
    else
      echo "No artisan on current PATH"
    fi
  }
fi

if is_binary nala; then
  function npt {
    case "$1" in
    [iI] | [iI]nstall)
      sudo nala install "${@:2}"
      ;;
    [uU] | [uU]pdate)
      sudo nala update "${@:2}"
      ;;
    [uU]g | [uU]pgrade)
      sudo nala upgrade "${@:2}"
      ;;
    [sS] | [sS]earch)
      sudo nala search "${@:2}"
      ;;
    [rR] | [rR]emove)
      sudo nala remove "${@:2}"
      ;;
    l | ls | list)
      sudo nala list --installed
      ;;
    [hH] | [hH]elp)
      nala --help
      ;;
    *)
      sudo nala "${@}"
      ;;
    esac
  }
fi

if is_binary pacstall; then
  function pt {
    case "$1" in
    [iI] | [iI]nstall)
      sudo pacstall -I "${@:2}"
      ;;
    [uU] | [uU]pdate)
      sudo pacstall -U "${@:2}"
      ;;
    [uU]g | [uU]pgrade)
      sudo pacstall -Up "${@:2}"
      ;;
    [sS] | [sS]earch)
      sudo pacstall -S "${@:2}"
      ;;
    [rR] | [rR]emove)
      sudo pacstall -R "${@:2}"
      ;;
    l | ls | list)
      sudo pacstall -L
      ;;
    [hH] | [hH]help)
      pacstall -h
      ;;
    *)
      sudo pacstall "${@}"
      ;;
    esac
  }
fi

if is_binary git; then
  function gcm {
    if ! git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
      return 0
    fi

    commit_prefix="$(git branch --show-current | awk 'match($0, /[A-Z]+-[0-9]+/) { print substr($0, RSTART, RLENGTH) }')"
    if [[ "$commit_prefix" = "" ]]; then
      commit_prompt="Message: "
    else
      commit_prompt="Message for $commit_prefix: "
    fi
    commit_message=$(fzf --height=1 --reverse --no-info --print-query --prompt="$commit_prompt" </dev/null | head -1)
    if [[ "$commit_prefix" = "" ]]; then
      git commit -m "$commit_message"
    else
      git commit -m "$commit_prefix: $commit_message"
    fi
  }

  function gp {
    if ! git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
      return 0
    fi

    banned_branches=("develop" "staging" "production" "master")
    branch_name="$(git branch --show-current)"

    if [[ " ${banned_branches[@]} " =~ " ${branch_name} " ]]; then
      echo "You are about to push to a protected branch [$branch_name]."

      choice=$(echo -e "No\nYes" | fzf --no-input --height=2 --no-info --select-1 --exit-0)
      if [[ "$choice" != "Yes" ]]; then
        echo "Push aborted on $branch_name"
        return 1
      fi
    fi
    git push origin "$branch_name"
  }

  function gcb {
    if ! git rev-parse --is-inside-work-tree >/dev/null 2>&1; then
      return 0
    fi

    local branch_name
    branch_name=$(git branch --all | sed -E 's/^[ *]*//' | fzf --prompt="Search branch: " --query="" --select-1 --exit-0 --print-query --no-info --height=5 | tail -n1)

    if [[ -n "$branch_name" && $(git branch --all | sed -E 's/^[ *]*//' | grep -Fx "$branch_name") ]]; then
      branch_name=${branch_name#remotes/origin/}
      git checkout "$branch_name"
    else
      local branch_substring="$branch_name"
      local branch_prefix
      branch_prefix=$(echo -e "feature\nbugfix\nhotfix" | fzf --prompt="Select branch prefix: " --height=5 --no-info)

      local new_branch_name
      branch_suffix=$(fzf --height=1 --reverse --no-info --print-query --prompt="Enter your branch suffix: " </dev/null | head -1)

      if [[ -n "$branch_suffix" ]]; then
        branch_suffix=$(echo "$branch_suffix" | tr '[:upper:]' '[:lower:]' | sed 's/ /-/g')
        new_branch_name="$branch_prefix/$branch_substring-$branch_suffix"
      else
        new_branch_name="$branch_prefix/$branch_substring"
      fi

      if [[ "$new_branch_name" != "/" ]]; then
        git checkout -b "$new_branch_name"
      fi
    fi
  }

  function gd {
    git diff
  }
  function gst {
    git status
  }
  function gg {
    git pull
  }
  function GG {
    git pull --all
  }
  function gf {
    git fetch
  }
  function GF {
    git fetch --all
  }
  function ghr {
    git reset --hard HEAD~
  }
  function ghrn {
    git reset --hard HEAD~"$1"
  }
  function gr {
    git reset --soft HEAD~
  }
  function grn {
    git reset --soft HEAD~"$1"
  }
  function gw {
    git stash
    git stash clear
  }
fi

if is_binary bat; then
  function view_git_file {
    curl "$(echo "$1" | sed "s/github.com/raw.githubusercontent.com/g" | sed "s/\/blob\//\/refs\/heads\//g")" | bat -f -l "${1##*\.}"
  }
else
  function view_git_file {
    curl "$(echo "$1" | sed "s/github.com/raw.githubusercontent.com/g" | sed "s/\/blob\//\/refs\/heads\//g")" | cat
  }
fi

if is_binary eza; then
  alias ls='eza'
else
  alias ls='ls --color'
fi

if is_binary ss; then
  function pid_on_port {
    ss -lptn " sport = :$1"
  }
fi

if is_binary curl; then
  function color_swatches {
    curl -s "https://gist.githubusercontent.com/HaleTom/89ffe32783f89f403bba96bd7bcd1263/raw/e50a28ec54188d2413518788de6c6367ffcea4f7/print256colours.sh" | bash
  }
fi

if is_binary nvim; then
  alias vim='nvim'
  alias vi='nvim'
  alias nano='nvim'
  alias ed='nvim'
  alias svim='nvim -u NONE'
fi

if is_binary tmux; then
  function tls {
    bash -c "tmux ls"
  }

  function ta {
    if [ -z "$1" ]; then
      tmux attach
    else
      tmux attach -t "$1"
    fi
  }
fi

if is_binary screen; then
  function tx {
    session_name="$(git symbolic-ref --short HEAD 2>/dev/null)"
    case $1 in
    l | ls)
      screen -ls
      ;;
    n | new)
      screen -S "$session_name"
      ;;
    j | join)
      screen -r "$session_name"
      ;;
    *)
      screen "$@"
      ;;
    esac
  }
fi

if is_binary uv; then
  function pysrc {
    if ! is_file "$PWD/venv/bin/activate"; then
      uv venv ./venv
    fi
    source "$PWD/venv/bin/activate"
  }
fi

if is_binary fzf; then
  function fzf-project-picker {
    cd $(find ~/git -maxdepth 3 -mindepth 1 -type d -exec test -d '{}/.git' \; -print 2>/dev/null | fzf)
  }
fi
